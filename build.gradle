buildscript {
    ext {
        moduleVersion = project.hasProperty('moduleVersion') ? project.getProperty('moduleVersion') : '0.0.1-SNAPSHOT'
        uploaderUser = project.hasProperty('uploaderUser') ? project.getProperty('uploaderUser') : 'uploader'
        uploaderPassword = project.hasProperty('uploaderPassword') ? project.getProperty('uploaderPassword') : 'Set me with -P'
    }
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'
apply plugin: 'groovy'

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

jar {
    baseName = 'javelin-common-cmcs-utils'
    version = "${moduleVersion}"
}

javadoc { failOnError = false }

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourceJar(type: Jar) { from sourceSets.main.allJava }

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.mrll.javelin'
            artifactId 'javelin-common-cmcs-utils'
            version "${moduleVersion}"

            from components.java

            artifact sourceJar { classifier "sources" }
            artifact javadocJar
        }
    }
    repositories {
        maven {
            url "https://merrillcorp.jfrog.io/merrillcorp/javelin-libs"
            // Jenkins provides these as -P Gradle parameters.
            credentials {
                username = "${uploaderUser}"
                password = "${uploaderPassword}"
            }
        }
        mavenLocal()
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    maven {
        credentials {
            username "downloader"
            password "downloader"
        }
        url "https://merrillcorp.jfrog.io/merrillcorp/ds1"
    }
    mavenLocal()
}

dependencies {
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'
    compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.1'
    compile group: 'com.google.guava', name: 'guava', version: '24.0-jre'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.4'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.9.4'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'

    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.1-groovy-2.4'

}

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}
